plugins {
  id 'com.android.application'
}
// 定义变量，同学们注意：可以定义变量，也可以不定义变量
def app_android = this.getRootProject().ext.app_android
def app_dependencies = this.rootProject.ext.app_dependencies
android {
  compileSdkVersion app_android.compileSdkVersion

  defaultConfig {
    applicationId app_android.applicationId
    minSdkVersion app_android.minSdkVersion
    targetSdkVersion app_android.targetSdkVersion
    versionCode app_android.versionCode
    versionName app_android.versionName

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    // 这个方法接收三个非空的参数，第一个：确定值的类型，第二个：指定key的名字，第三个：传值（必须是String）
    // 为什么需要定义这个？因为src代码中有可能需要用到跨模块交互，如果是组件化模块显然不行
    // 切记：不能在android根节点，只能在defaultConfig或buildTypes节点下
    buildConfigField("boolean", "isRelease", String.valueOf(isRelease))


    // 都是为了 传递给 注解处理器
    // 在gradle文件中配置选项参数值（用于APT传参接收）
    // 切记：必须写在defaultConfig节点下
    javaCompileOptions {
      annotationProcessorOptions {
        // this.project.getName() == app
        // this.getProject().getName() == app
        arguments = [moduleName: project.getName(), packageNameForAPT: packageNameForAPT]
      }
    }
  }

  buildTypes {
    debug {
      buildConfigField("String", "debug", "\"${url.debug}\"")
    }
    release {
      buildConfigField("String", "release", "\"${url.release}\"")
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  namespace 'com.wuc.moduleproject'
}

dependencies {

  implementation fileTree(dir: "libs", include: ["*.jar"])

  /* implementation 'androidx.appcompat:appcompat:1.2.0'
   implementation 'com.google.android.material:material:1.2.1'
   implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
   testImplementation 'junit:junit:4.13.1'
   androidTestImplementation 'androidx.test.ext:junit:1.1.2'
   androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'*/

  // 循环引入第三方库
  app_dependencies.each { k, v -> implementation v }

  // 公共基础库
  implementation project(":common")
  // 注解模块
  implementation project(":arouter_annotation")

  // 注解处理器
  annotationProcessor project(':arouter_compiler')


  // 如果是集成化模式，做发布版本时。各个模块都不能独立运行了
  if (isRelease) {
    implementation project(':order') // 这样依赖时，必须是集成化，有柱状图， 否则会循环依赖问题
    implementation project(':personal') // 这样依赖时，必须是集成化，有柱状图， 否则会循环依赖问题
  }
}